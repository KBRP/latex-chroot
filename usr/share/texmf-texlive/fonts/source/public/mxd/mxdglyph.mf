%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% File: mxdglyph.mf
% Date: Nov. 5th, 2001
% Author: Oliver Corff
% (c) 2001 Ulaanbaatar, Berlin
%
% A metafont for providing the Xewtee
% Dorboljin script to LaTeX-users.
%
% Usage: see package drbl4tex
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% This font contains the Xewtee Dorboljin alphabet. %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%%%%%%%%% Character definitions. %%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(MXD_INIT,fullcw#,vheight#+14thin#,depth#); %
	"Xewtee Dorboljin Initial";
	squarepen;
	top lft z1=(scribble+margin,height+scribble);
	bot     z2=(scribble+x1,0+scribble);
	bot rt  z3=(scribble+cwidth+margin,0+scribble);
		z4=(scribble+x3,y1+scribble);
		z5=(scribble+1/2[x1,x4],y1+scribble);
		z6=(scribble+x5,1/2[y1,y2]+scribble);
	draw z1--z2--z3--z4--z5--z6;	

	% Mond
	z26=(scribble+x1,vheight+scribble);
	z27=(scribble+x5,height+thin+scribble);
	z28=(scribble+x3,y26+scribble);
	z29=(scribble+x5,y27+1.1thin+scribble);
	fill z26..z27..z28 & z28..z29..z26 & cycle;

	% Sonne
	pickup pencircle scaled 0.8thin;
	z30=(scribble+x5,y29+thin+scribble);
	z31=(scribble+x5,y30+4thin+scribble);
	draw z30..z31..cycle;

	% Feuer
	z32=(scribble+x5,y31+5thin+thin+scribble);
	penpos32(epsilon,0); %
	z33=(scribble+x5-1.5thin,y32-1.5thin+scribble);
	penpos33(.75thin,0); %
	z34=(scribble+x5,y31+2thin+scribble);
	penpos34(.75thin,90); %
	z35=(scribble+x5+1.5thin,y33+thin+scribble);
	penpos35(.75thin,180); %
	z36=(scribble+x5,y32+4thin+scribble);
	penpos36(epsilon,180); %
	penstroke z32e{down}..z33e..z34e..z35e{dir 135}..{up}z36e;
endchar;

beginchar(MXD_STOP,2margin#+thick#,height#+2pt#,0); %
	"Punctuation";
	squarepen;
	top lft z1=(scribble+margin,height+scribble);
	bot z2=(scribble+x1,0+scribble);
	draw z1--z2;	
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Der leere Vokaltraeger hat seinen eigenen Satz Ligaturen!
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_DVC,fullaw#,height#+2pt#,depth#); %
	"Dummy Vowel Carrier";
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVI, fullaw#,vheight#+2pt#,depth#); %
	"Empty Vowel I";
	vowel_i(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVE, fullaw#,vheight#+2pt#,depth#); %
	"Empty Vowel E";
	vowel_e(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVUE, fullaw#,height#+2pt#,depth#); %
	"Empty Vowel UE";
	vowel_ue(scribble+margin,margin+awidth,0+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVU, fullaw#,height#+2pt#,depth#); %
	"Empty Vowel U";
	vowel_u(scribble+margin,margin+awidth,0+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVO, fullaw#,vheight#+2pt#,depth#); %
	"Empty Vowel O";
	vowel_o(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

beginchar(MXD_EVOE, fullaw#,vheight#+2pt#,depth#); %
	"Empty Vowel OE";
	vowel_oe(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
	empty_vowel;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Der Vokaltraeger hat seinen eigenen Satz Ligaturen!
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_VC, fullaw#,height#+2pt#,depth#); %
	"Vowel Carrier";
	vowelcarrier;
endchar;

beginchar(MXD_VI, fullaw#,vheight#+2pt#,depth#); %
	"Vowel I";
	vowel_i(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
endchar;

beginchar(MXD_VE, fullaw#,vheight#+2pt#,depth#); %
	"Vowel E";
	vowel_e(scribble+margin,margin+awidth+scribble);
	vowelcarrier;
endchar;

beginchar(MXD_VUE, fullaw#,height#+2pt#,depth#); %
	"Vowel UE";
	vowel_ue(scribble+margin,margin+awidth,0+scribble);
	vowelcarrier;
endchar;

beginchar(MXD_VU, fullaw#,height#+2pt#,depth#); %
	"Vowel U";
	vowel_u(scribble+margin,margin+awidth,0+scribble);
	vowelcarrier;
endchar;

beginchar(MXD_VO, fullaw#,height#+2pt#,depth#); %
	"Vowel O";
	vowelcarrier;
	vowel_o(scribble+margin,margin+awidth+scribble);
endchar;

beginchar(MXD_VOE, fullaw#,height#+2pt#,depth#); %
	"Vowel OE";
	vowelcarrier;
endchar;

beginchar(MXD_LVM,0,0,depth#); %
	"Long Vowel Marker";
	z1l=(scribble-margin,0+scribble);
	penpos1(thick,90); %
	z2 =(scribble+3/4margin,-depth+scribble);
	penpos2(3/4thin,90); %
	penstroke z1e--z2e;
endchar;

beginchar(MXD_I,0,vheight#+2pt#,0); %
	"Vowel i";
	vowel_i(scribble-margin-cwidth,-margin+scribble);
endchar;

beginchar(MXD_E,0,vheight#+2pt#,0); %
	"Vowel e";
	vowel_e(scribble-margin-cwidth,-margin+scribble);
endchar;

beginchar(MXD_UE,0,height#,0); %
	"Vowel ue";
	vowel_ue(scribble-margin-cwidth,-margin,0+scribble);
endchar;

beginchar(MXD_U,0,height#,0); %
	"Vowel u";
	vowel_u(scribble-margin-cwidth,-margin,0+scribble);
endchar;

beginchar(MXD_O,0,vheight#+2pt#,0); %
	"Vowel o";
	vowel_o(scribble-margin-cwidth,-margin+scribble);
endchar;

beginchar(MXD_OE,0,vheight#+2pt#,0); %
	"Umlaut o";
	vowel_oe(scribble-margin-cwidth,-margin+scribble);
endchar;

beginchar(MXD_DU,0,vheight#+2pt#,0); %
	"Undefined: diphtong u";
endchar;

beginchar(MXD_DI,0,vheight#+2pt#,0); %
	"Undefined: diphtong i";
endchar;

beginchar(MXD_G,fullcw#,height#+2pt#,depth#); %
	"Consonant k";
	squarepen;
	top lft z1=(scribble+margin,height+scribble);
	top rt  z2=(scribble+cwidth+margin,height+scribble);
	bot     z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,1/2height+scribble);
		z5=(scribble+x1,y3+scribble);
		z6=(scribble+1/2[x1,x2],y3+scribble);
		z7=(scribble+x6,y1+scribble);
	draw z1--z2--z3;
	draw z4--z5--z6--z7;
endchar;

beginchar(MXD_K,fullcw#,height#+2pt#,depth#); %
	"Consonant k";
	squarepen;
	bot lft	z1=(scribble+margin,0+scribble);
	top	z2=(scribble+x1,height+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x3,y1+scribble);
		z5=(scribble+1/2[x2,x3],y1+scribble);
		z6=(scribble+x5,y2+scribble);
	draw z1--z2--z3--z4--z5--z6;	
endchar;

beginchar(MXD_DN,fullcw#,height#+2pt#,depth#); %
	"Consonant \d n";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
		z3=(scribble+x2,1/2height+scribble);
		z4=(scribble+x1,y3+scribble);
	bot	z5=(scribble+x1,0+scribble);
		z6=(scribble+x2,y5+scribble);
	draw z1--z2--z3--z4--z5--z6;	
endchar;

beginchar(MXD_Z,fullcw#,height#+2pt#,depth#); %
	"Consonant z";
	squarepen;
	z11l=(scribble+margin+cwidth,height+scribble);
	z12 =(scribble+margin+cwidth+1/2margin,height+2n+scribble);
	z13 =(scribble+x12+n,y12+scribble);
	z14r=(scribble+x13+n,vheight+scribble);
	penpos11(.25thin,90); %
	penpos12(.75thick,90); %
	penpos13(.75thick,90); %
	penpos14(.25thin,90); %
	penstroke z11e--z12e--z13e--z14e;

	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+1/2[x1,x2],y2+scribble);
		z6=(scribble+x5,y3+scribble);
	draw z1--z2--z3--z4; draw z5--z6;	
endchar;

beginchar(MXD_C,fullcw#,vheight#,depth#); %
	"Consonant c";
	z11l=(scribble+margin+cwidth,height+scribble);
	z12 =(scribble+margin+cwidth+1/2margin,height+2n+scribble);
	z13 =(scribble+x12+n,y12+scribble);
	z14r=(scribble+x13+n,vheight+scribble);
	penpos11(.25thin,90); %
	penpos12(.75thick,90); %
	penpos13(.75thick,90); %
	penpos14(.25thin,90); %
	penstroke z11e--z12e--z13e--z14e;

	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+1/2[x1,x2],y2+scribble);
		z6=(scribble+x5,y3+scribble);
	draw z1--z2--z3--z4--cycle; draw z5--z6;	
endchar;

beginchar(MXD_NNN,fullcw#,height#+2pt#,depth#); %
	"Consonant \= n";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x2,1/2height+scribble);
		z5=(scribble+x1,y4+scribble);
		z6=(scribble+x1,y3+scribble);
		z7=(scribble+x2-2thick,y6+scribble);
	draw z1--z2--z3; draw z4--z5--z6--z7;	
endchar;

beginchar(MXD_D,fullcw#,height#+2pt#,depth#); %
	"Consonant d";
	squarepen;
	bot lft	z1=(scribble+margin,0+scribble);
	top 	z2=(scribble+x1,height+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x1,1/2[y1,y2]+scribble);
		z5=(scribble+x3,y4+scribble);
		z6=(scribble+x3,y1+scribble);
	draw z1--z2--z3; draw z4--z5--z6;	
endchar;

beginchar(MXD_T,fullcw#,height#+2pt#,depth#); %
	"Consonant t";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+x1,1/2[y2,y3]+scribble);
		z6=(scribble+x2,y5+scribble);
	draw z1--z2--z3--z4--cycle; draw z5--z6;	
endchar;

beginchar(MXD_N,fullcw#,height#+2pt#,depth#); %
	"Consonant n";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x2,1/2height+scribble);
		z5=(scribble+x1,y4+scribble);
		z6=(scribble+x1,y3+scribble);
		z7=(scribble+1/2[x1,x2],y6+scribble);
		z8=(scribble+x7,y4+scribble);
	draw z1--z2--z3; draw z4--z5--z6--z7--z8;	
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Das "b" hat seinen eigenen Satz Ligaturen!
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_B,fullbw#,height#+2pt#,depth#); %
	"Consonant b";
	b;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_LIGBI,fullbw#,vheight#+2pt#,depth#); %
	"Ligature bi";
	vowel_i(scribble+margin,margin+bwidth+scribble);
	b;
endchar;

beginchar(MXD_LIGBE,fullbw#,vheight#+2pt#,depth#); %
	"Ligature be";
	vowel_e(scribble+margin,margin+bwidth+scribble);
	b;
endchar;

beginchar(MXD_LIGBUE,fullbw#,height#+2pt#,depth#); %
	"Ligature bue";
	vowel_ue(scribble+margin,margin+bwidth,0+scribble);
	b;
endchar;

beginchar(MXD_LIGBU,fullbw#,height#+2pt#,depth#); %
	"Ligature bu";
	vowel_u(scribble+margin,margin+bwidth,0+scribble);
	b;
endchar;

beginchar(MXD_LIGBO,fullbw#,vheight#+2pt#,depth#); %
	"Ligature bo";
	vowel_o(scribble+margin,margin+bwidth+scribble);
	b;
endchar;

beginchar(MXD_LIGBOE,fullbw#,vheight#+2pt#,depth#); %
	"Ligature boe";
	vowel_oe(scribble+margin,margin+bwidth+scribble);
	b;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(MXD_P,fullcw#,height#+2pt#,depth#); %
	"Consonant p";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	bot 	z2=(scribble+x1,0+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x3,y1+scribble);
		z5=(scribble+1/2[x2,x3],y1+scribble);
		z6=(scribble+x5,y2+scribble);
	draw z1--z2--z3--z4--z5--z6;	
endchar;

beginchar(MXD_M,fullcw#,height#+2pt#,depth#); %
	"Consonant m";
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	bot 	z2=(scribble+x1,0+scribble);
	    lft	z3=(scribble+margin,y2+scribble);
		z4=(scribble+x3,y1+scribble);
		z5=(scribble+1/2[x2,x3],y1+scribble);
		z6=(scribble+x5,y2+scribble);
	draw z1--z2--z3--z4--z5--z6;	
endchar;

beginchar(MXD_Y,fullcw#,height#+2pt#,depth#); %
	"Consonant y";
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	bot 	z2=(scribble+x1,0+scribble);
	    lft	z3=(scribble+margin,y2+scribble);
		z4=(scribble+x3,y1+scribble);
		z5=(scribble+1/2[x2,x3],y1+scribble);
		z6=(scribble+x5,y2+scribble);
	draw z1--z2--z3--z4; draw z5--z6;	
endchar;

beginchar(MXD_R,fullcw#,height#+2pt#,depth#); %
	"Consonant r";
	basic_r;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Das "w" hat seinen eigenen Satz Ligaturen!
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_W,fullbw#,height#+2pt#,depth#); %
	"Consonant w";
	basic_w;
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MXD_LIGWI,fullbw#,vheight#+2pt#,depth#); %
	"Ligature wi";
	vowel_i(scribble+margin,margin+bwidth+scribble);
	basic_w;
endchar;

beginchar(MXD_LIGWE,fullbw#,vheight#+2pt#,depth#); %
	"Ligature we";
	vowel_e(scribble+margin,margin+bwidth+scribble);
	basic_w;
endchar;

beginchar(MXD_LIGWUE,fullbw#,height#+2pt#,depth#); %
	"Ligature wue";
	vowel_ue(scribble+margin,margin+bwidth,0+scribble);
	basic_w;
endchar;

beginchar(MXD_LIGWU,fullbw#,height#+2pt#,depth#); %
	"Ligature wu";
	vowel_u(scribble+margin,margin+bwidth,0+scribble);
	basic_w;
endchar;

beginchar(MXD_LIGWO,fullbw#,vheight#+2pt#,depth#); %
	"Ligature wo";
	vowel_o(scribble+margin,margin+bwidth+scribble);
	basic_w;
endchar;

beginchar(MXD_LIGWOE,fullbw#,vheight#+2pt#,depth#); %
	"Ligature woe";
	vowel_oe(scribble+margin,margin+bwidth+scribble);
	basic_w;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(MXD_L,fullcw#,height#+2pt#,depth#); %
	"Consonant l";
	basic_l;
endchar;

beginchar(MXD_SH,fullcw#,height#+2pt#,depth#); %
	"Consonant sh";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	bot	z2=(scribble+x1,0+scribble);
		z3=(scribble+x1,1/2height+scribble);
		z4=(scribble+1/2[x1,x8],y3+scribble);
		z5=(scribble+x4,y1+scribble);
		z6=(scribble+x4,y2+scribble);
	    rt  z7=(scribble+margin+cwidth,y2+scribble);
		z8=(scribble+x7,y1+scribble);
	draw z1--z2; draw z3--z4; draw z5--z6--z7--z8--cycle;
endchar;

beginchar(MXD_S,fullcw#,height#+2pt#,depth#); %
	"s";
	z1l=(scribble+margin,0+scribble);
	z2l=(scribble+margin,height+scribble);
	penpos1(thick,0); %
	penpos2(thick,0); %
	penstroke z1e--z2e;
	z3l=(scribble+margin+thin,height+scribble);
	z4r=(scribble+margin+cwidth-thin,0+scribble);
	penpos3(.9thick,0); %
	penpos4(.9thick,0); %
	penstroke z3e--z4e;
	z5r=(scribble+margin+cwidth,0+scribble);
	z6r=(scribble+margin+cwidth,height+scribble);
	penpos5(thick,0); %
	penpos6(thick,0); %
	penstroke z5e--z6e;
endchar;

beginchar(MXD_H,fullcw#,height#+2pt#,depth#); %
	"h";
	squarepen;
	top rt  z1=(scribble+margin+cwidth,height+scribble);
	    lft z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+1/2[x1,x2],y3+scribble);
		z5=(scribble+x4,1/2height+scribble);
		z6=(scribble+x1,y5+scribble);
		z7=(scribble+x1,y3+scribble);
	draw z1--z2--z3--z4--z5--z6--z7;
endchar;

beginchar(MXD_KDS,fullcw#,height#+2pt#,depth#); %
	"k\d s";
	squarepen;
	top lft z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+1/2[x1,x2],y1+scribble);
		z5=(scribble+x4,1/2height+scribble);
		z6=(scribble+x1,y5+scribble);
		z7=(scribble+x1,y3+scribble);
		z8=(scribble+x4,y3+scribble);
	draw z1--z2--z3; draw z4--z5--z6--z7--z8;
endchar;

beginchar(MXD_A,fullaw#,height#,0); %
	"Undefined Vowel a";
endchar;

beginchar(MXD_LIGRI,fullcw#,vheight#+2pt#,depth#); %
	"Ligature ri";
	vowel_i(scribble+margin,margin+cwidth+scribble);
	basic_r;
endchar;

beginchar(MXD_LIGLI,fullcw#,vheight#+2pt#,depth#); %
	"Ligature li";
	vowel_i(scribble+margin,margin+cwidth+scribble);
	basic_l;
endchar;

beginchar(MXD_OM,0,om_height#+2pt#,0); %
	"Undefined OM";
endchar;

beginchar(MXD_EH,fullaw#,height#+2pt#,0); %
	"Undefined";
endchar;

beginchar(MXD_SG,fullcw#,height#+2pt#,depth#); %
	"Consonant \sg";
	squarepen;
	top	z1=(scribble+1/2[x4,x5],height+scribble);
	bot	z2=(scribble+x1,0+scribble);
	    lft z3=(scribble+margin,y2+scribble);
		z4=(scribble+x3,y1+scribble);
	    rt	z5=(scribble+margin+cwidth,y1+scribble);
		z6=(scribble+x5,y2+scribble);
	draw z1--z2--z3--z4--z5--z6;
endchar;

beginchar(MXD_GH,fullcw#,height#+2pt#,depth#); %
	"Consonant gh";
	squarepen;
	top	z1=(scribble+1/2[x6,x7],height+scribble);
	bot	z2=(scribble+x1,0+scribble);
	    lft z3=(scribble+margin,y2+scribble);
		z4=(scribble+x3,1/2height+scribble);
		z5=(scribble+x1,1/2height+scribble);
		z6=(scribble+x3,y1+scribble);
	    rt	z7=(scribble+margin+cwidth,y1+scribble);
		z8=(scribble+x7,y2+scribble);
	draw z1--z2--z3--z4--z5; draw z6--z7--z8;
endchar;

beginchar(MXD_J,fullcw#,vheight#+2pt#,depth#); %
	"Consonant j";
	z11l=(scribble+margin+cwidth,height+scribble);
	z12 =(scribble+margin+cwidth+1/2margin,height+2n+scribble);
	z13 =(scribble+x12+2n,y12+scribble);
	z14r=(scribble+x13+n,vheight+scribble);
	penpos11(.25thin,90); %
	penpos12(.75thick,90); %
	penpos13(.75thick,90); %
	penpos14(.25thin,90); %
	penstroke z11e--z12e--z13e--z14e;
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft	z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+x2,1/2height+scribble);
		z6=(scribble+x1,y5+scribble);
	draw z1--z2--z3--z4; draw z5--z6;
endchar;

beginchar(MXD_JH,fullcw#,vheight#+2pt#,depth#); %
	"Consonant jh";
	z11l=(scribble+margin+cwidth,height+scribble);
	z12 =(scribble+margin+cwidth+1/2margin,height+2n+scribble);
	z13 =(scribble+x12+2n,y12+scribble);
	z14r=(scribble+x13+n,vheight+scribble);
	penpos11(.25thin,90); %
	penpos12(.75thick,90); %
	penpos13(.75thick,90); %
	penpos14(.25thin,90); %
	penstroke z11e--z12e--z13e--z14e;
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft	z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+x2,1/2height+scribble);
		z6=(scribble+7/12[x2,x1],y5+scribble);
	draw z1--z2--z3--z4; draw z5--z6;
		z7r=(scribble+margin+cwidth,2/3[y3,y1]+scribble);
		z8r=(scribble+x6+1/3thick,1/2[y7,y9]+scribble);
		z9r=(scribble+margin+cwidth,1/3[y3,y1]+scribble);
	penpos7(thin,45); %
	penpos8(thin,180); %
	penpos9(thin,315); %
	penstroke z7e..z8e..z9e;
endchar;

beginchar(MXD_DT,fullcw#,height#+2pt#,depth#); %
	"Consonant \d t";
	z1=(scribble+margin,7/12height+scribble);
	penpos1(thick,90); %
	z2=(scribble+margin+cwidth-thick,y1+scribble);
	penpos2(thick,90); %
	z3r=(scribble+margin+cwidth,y1+1/2thick+scribble);
	penpos3(thick,0); %
	z4 =(scribble+1/2[x1,x2],0+scribble);
	penpos4(thick,0); %
	penstroke z1e--z2e;
	penstroke z3e--z4e;
	
	numeric x[]; numeric y[];
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft	z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
	draw z1--z2--z3;
endchar;

beginchar(MXD_DTH,fullcw#,height#+2pt#,depth#); %
	"Consonant \d th";
	z1l=(scribble+margin,height+scribble);
	penpos1(thick,0); %
	z2r=(scribble+margin+cwidth,0+scribble);
	penpos2(thick,0); %
	penstroke z1e--z2e;

	z3r=(scribble+margin+cwidth,height+scribble);
	penpos3(thick,0); %
	z4l=(scribble+margin,0+scribble);
	penpos4(thick,0); %
	penstroke z3e--z4e;

	z5r=(scribble+margin+thick,height+scribble);
	penpos5(thick,90); %
	z6r=(scribble+margin+cwidth-thick,height+scribble);
	penpos6(thick,90); %
	penstroke z5e--z6e;

	z7l=(scribble+margin+thick,0+scribble);
	penpos7(thick,90); %
	z8l=(scribble+margin+cwidth-thick,0+scribble);
	penpos8(thick,90); %
	penstroke z7e--z8e;
endchar;

beginchar(MXD_DD,fullcw#,height#+2pt#,depth#); %
	"Consonant \d d";
	z1l=(scribble+margin,height+scribble);
	penpos1(thick,0); %
	z2r=(scribble+margin+cwidth,0+scribble);
	penpos2(thick,0); %
	penstroke z1e--z2e;

	z5r=(scribble+margin+thick,height+scribble);
	penpos5(thick,90); %
	z6r=(scribble+margin+cwidth,height+scribble);
	penpos6(thick,90); %
	penstroke z5e--z6e;

	z7l=(scribble+margin,0+scribble);
	penpos7(thick,90); %
	z8l=(scribble+margin+cwidth-thick,0+scribble);
	penpos8(thick,90); %
	penstroke z7e--z8e;
endchar;

beginchar(MXD_DDH,fullcw#,height#+2pt#,depth#); %
	"Consonant \d dh";
	z1l=(scribble+margin,height+scribble);
	penpos1(thick,0); %
	z2r=(scribble+margin+cwidth,0+scribble);
	penpos2(thick,0); %
	penstroke z1e--z2e;

	z3l=(scribble+margin,0+scribble);
	penpos3(thick,90); %
	z4l=(scribble+margin+cwidth-thick,0+scribble);
	penpos4(thick,90); %
	penstroke z3e--z4e;

	squarepen;
	top lft z5=(scribble+margin+thick,height+scribble);
	    rt	z6=(scribble+margin+cwidth,y5+scribble);
	bot	z7=(scribble+x6,height-2thick+scribble);
	draw z5--z6--z7;
endchar;

beginchar(MXD_DDN,fullcw#,height#+2pt#,depth#); %
	"Consonant \d n";
	squarepen;
	top lft z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
	draw z1--z2--z3;
	
	z4 =(scribble+x2,9/12height+scribble);
	z5l=(scribble+margin,4.5/12height+scribble);
	penpos4(thick,-30); %
	penpos5(thick,-30); %
	penstroke z4e--z5e;
	z6l=(scribble+x2-1/2thick,0+scribble);
	z7l=(scribble+margin,4.5/12height+scribble);
	penpos6(thick,30); %
	penpos7(thick,30); %
	penstroke z6e--z7e;
endchar;

beginchar(MXD_DDD,fullcw#,height#+2pt#,depth#); %
	"Consonant \_d";
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft z2=(scribble+margin,y1+scribble);
		z3=(scribble+x2,1/2height+scribble);
		z4=(scribble+x1,y3+scribble);
	bot	z5=(scribble+x1,0+scribble);
		z6=(scribble+x2,y5+scribble);
	draw z1--z2--z3--z4--z5--z6;
endchar;

beginchar(MXD_DDDH,fullcw#,height#+2pt#,depth#); %
	"Consonant \_dh";
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x2,1/2height+scribble);
		z5=(scribble+x1,y4+scribble);
	draw z1--z2--z3; draw z4--z5;
		z11=(scribble+margin+cwidth,y4+1/2thick+scribble);
		z12=(scribble+5/12[x2,x1],1/2[y11,y13]+scribble);
		z13=(scribble+x11,0+scribble);
	fill z11{left}..z12..{right}z13 & z13--z11 & cycle;
		z14=(scribble+x11,y11-1/3thin+scribble);
		z15=(scribble+x12+4/4thick,y12+scribble);
		z16=(scribble+x11,y13+1/3thin+scribble);
	erase unfill z14{left}..z15..{right}z16 & z16--z14 & cycle;
endchar;

beginchar(MXD_DB,fullcw#,height#+2pt#,depth#); %
	"Consonant _b";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	bot 	z2=(scribble+x1,0+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x3,y1+scribble);
	draw z1--z2--z3--z4--cycle;	
endchar;

beginchar(MXD_DBH,fullcw#,height#+2pt#,depth#); %
	"Consonant \_bh";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot 	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
	draw z1--z2--z3--z4;	
		z11=(scribble+margin+thick,height+scribble);
		z12=(scribble+6/12[x1,x2],1/2height+scribble);
		z13=(scribble+x11,0+scribble);
		z14=(scribble+margin,y11+scribble);
		z15=(scribble+x12-thick,y12+scribble);
		z16=(scribble+x14,0+scribble);
	fill z11..z12..z13 & z13--z16--z14--z11--cycle;
	erase fill z14..z15..z16 & z16--z14 & cycle;
endchar;

beginchar(MXD_DS,fullcw#,height#+2pt#,depth#); %
	"Consonant \d s";
	squarepen;
	top rt	z1=(scribble+margin+cwidth,height+scribble);
	    lft	z2=(scribble+margin,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+1/2[x2,x1]+2/3thick,y1+scribble);
		z6=(scribble+x5,7.5/12height+scribble);
		z7r=(scribble+1/2[x2,x1]+2/3thick,7.25/12height+scribble);
		z8r=(scribble+x7,0+scribble);
	draw z1--z2--z3; draw z5--z6;

	penpos7(3/5thick,90); %
	penpos8(3/5thick,270); %
	penstroke z7e..z8e..cycle;
endchar;

beginchar(MXD_KY,fullcw#,height#+2pt#,depth#+2pt#); %
	"Ligature kya";
	kya;
endchar;

beginchar(MXD_LIGKYUE,fullcw#,height#+2pt#,depth#+3pt#); %
	"Ligature kyue";
	vowel_ue(scribble+margin,cwidth+margin,3/4depth+scribble);
	kya;
endchar;

beginchar(MXD_LIGKYU,fullcw#,height#+2pt#,depth#+3pt#); %
	"Ligature kyu";
	vowel_u(scribble+margin,cwidth+margin,5/8depth+scribble);
	kya;
endchar;

beginchar(MXD_KR,fullcw#,height#+2pt#,depth#+2pt#); %
	"Ligature kra";
	kra;
endchar;

beginchar(MXD_KRUE,fullcw#,height#+2pt#,depth#+4pt#); %
	"Ligature krue";
	vowel_ue(scribble+margin,cwidth+margin,depth+scribble);
	kra;
endchar;

beginchar(MXD_KRU,fullcw#,height#+2pt#,depth#+4pt#); %
	"Ligature kru";
	vowel_u(scribble+margin,cwidth+margin,depth+scribble);
	kra;
endchar;

beginchar(MXD_KL,fullcw#,height#+2pt#,depth#+2pt#); %
	"Ligature kla";
	kla;
endchar;

beginchar(MXD_KLUE,fullcw#,height#+2pt#,depth#+3pt#); %
	"Ligature klue";
	vowel_ue(scribble+margin,cwidth+margin,6/8depth+scribble);
	kla;
endchar;

beginchar(MXD_KLU,fullcw#,height#+2pt#,depth#+4pt#); %
	"Ligature klu";
	vowel_u(scribble+margin,cwidth+margin,depth+scribble);
	kla;
endchar;

beginchar(MXD_KM,fullaw#,height#+2pt#,depth#); %
	"Undefined Ligature km";
endchar;

beginchar(MXD_KK,fullaw#,height#+2pt#,depth#); %
	"Undefined Ligaute kk";
endchar;

beginchar(MXD_DNK,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_NC,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_DDNT,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_NT,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_MP,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_LK,fullcw#,height#+2pt#,depth#); %
	"lka";
	squarepen;
	top lft z1=(scribble+margin,29/48height+scribble);
	    rt  z2=(scribble+cwidth+margin,y1+scribble);
	bot     z3=(scribble+x2,0+scribble);
	top	z4=(scribble+x1,2/6height+scribble);
		z5=(scribble+x1,y3+scribble);
		z6=(scribble+1/2[x1,x2],y3+scribble);
	top	z7=(scribble+x6,height+scribble);
		z8=(scribble+x2,y7+scribble);
		z9=(scribble+x1,19/24height+scribble);
		z10=(scribble+x1,y7+scribble);
	draw z1--z2--z3;
	draw z4--z5--z6--z7;
	draw z8--z2;
	draw z9--z10--z7;
endchar;

beginchar(MXD_SHK,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_SK,fullcw#,height#+2pt#,depth#); %
	"Ligature ska";
	squarepen;
	top lft z1=(scribble+margin,29/48height+scribble);
	    rt  z2=(scribble+cwidth+margin,y1+scribble);
	bot     z3=(scribble+x2,0+scribble);
	top	z4=(scribble+x1,2/6height+scribble);
		z5=(scribble+x1,y3+scribble);
		z6=(scribble+1/2[x1,x2],y3+scribble);
		z7=(scribble+x6,y1+scribble);
	top	z8=(scribble+x2,height+scribble);
		z9=(scribble+x1,19/24height+scribble);
		z10=(scribble+x1,y8+scribble);
	draw z1--z2--z3;
	draw z4--z5--z6--z7;
	draw z8--z2;
	draw z9--z10;
		z11l=(scribble+margin+1/4thick,height+scribble);
		z12r=(scribble+margin+cwidth,y1+1/2thick+scribble);
	penpos11(1.00thick,0); %
	penpos12(1.00thick,0); %
	penstroke z11e--z12e;
endchar;

beginchar(MXD_RK,fullcw#,height#+2pt#,depth#); %
	"Ligature rka";
	squarepen;
	top lft z1=(scribble+margin,4/6height+scribble);
	    rt  z2=(scribble+cwidth+margin,y1+scribble);
	bot     z3=(scribble+x2,0+scribble);
	top	z4=(scribble+x1,2/6height+scribble);
		z5=(scribble+x1,y3+scribble);
		z6=(scribble+1/2[x1,x2],y3+scribble);
	top	z7=(scribble+x6,height+scribble);
	draw z1--z2--z3;
	draw z4--z5--z6--z7;
	draw (scribble+x1,y7)--(scribble+x2,y7+scribble);
endchar;

beginchar(MXD_ZZ,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_CC,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_EJ,fullaw#,height#+2pt#,depth#); %
	"Undefined";
endchar;

beginchar(MXD_X,fullcw#,height#+2pt#,depth#); %
	"Consonant X";
	squarepen;
	bot lft	z1=(scribble+margin,0+scribble);
	top	z2=(scribble+x1,height+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x3,y1+scribble);
	draw z1--z2--z3--z4;
	z5=(scribble+x3-1/2thick,2/3height+scribble);
	z6=(scribble+x1+1.75thick,1/2[y5,y7]+scribble);
	z7=(scribble+x5,1/3height-thick+scribble);
	penpos5(thin,90); %
	penpos6(.75thick,180); %
	penpos7(thin,270); %
	penstroke z5e{left}..z6e..{right}z7e;
endchar;

beginchar(MXD_Q,fullcw#,height#+2pt#,depth#); %
	"Consonant q";
	squarepen;
	top lft	z1=(scribble+margin,height+scribble);
	    rt	z2=(scribble+margin+cwidth,y1+scribble);
	bot	z3=(scribble+x2,0+scribble);
		z4=(scribble+x1,y3+scribble);
		z5=(scribble+x1,1/2[y2,y3]+scribble);
		z6=(scribble+x2,y5+scribble);
	draw z1--z2--z3--z4; draw z5--z6;
endchar;


beginchar(MXD_ACHUNG,fullcw#,height#+2pt#,depth#); %
	"'a (a chung)";
	squarepen;
	bot lft	z1=(scribble+margin,0+scribble);
	top	z2=(scribble+x1,height+scribble);
	    rt	z3=(scribble+margin+cwidth,y2+scribble);
		z4=(scribble+x3,1/2height+scribble);
		z5=(scribble+1/2[x2,x3],y4+scribble);
		z6=(scribble+x5,y1+scribble);
		z7=(scribble+x3,y1+scribble);
	draw z1--z2--z3--z4--z5--z6--z7;
endchar;
end.
% vim:ts=8
