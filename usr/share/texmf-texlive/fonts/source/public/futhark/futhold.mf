%  futhold.mf
%
%  Das \"Altere Futhark (The Older Futhark) 
%  Parameter File (1993/05/02)
%
%  Design by Micaela "Stayka" Pantke + Sigrid "Shavana" Juckel
%
%  Email:     hz225wu@unidui.uni-duisburg.de (Micaela Pantke)
%  Snailmail: Micaela Pantke
%             Westhoffstrasse 111
%             46149 Oberhausen
%             Germany
%
%  You're free to use or copy this file as long as you leave this 
%  header intact and don't change the contents.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

mode_setup;                 % switches into Metafont mode

ho#:=o#;                    % horizontal overshoot
leftstemloc#:=2.5u#+s#;     % position of left bottom lines
py#:=.9px#;                 % vertical thickness of the pen

define_pixels(s,u);
define_blacker_pixels(px,py);
pickup pencircle xscaled px yscaled py;
runen_pen:=savepen;
define_good_x_pixels(leftstemloc);
define_corrected_pixels(o);  % turns overshoot correction on
define_horizontal_corrected_pixels(ho);

def beginrunenchar(expr code, unit_width) =
 beginchar(code,unit_width*u#+2s#,ht#,0);
 pickup runen_pen enddef;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  There they are: the runes
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginrunenchar("F",20);
x1=x2=x3=x6=leftstemloc; x4=x5=w-s; 
bot y1=-o; y2=.4h; y3=.7h; y4=.6h; y5=.9h; top y6= h;
draw z1--z6;
draw z3--z5;
draw z2--z4;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("U",30);
x1=x2=leftstemloc; x3=x4=w-s; 
bot y1=bot y4=-o; y3=.6h; top y2=h; 
draw z1--z2--z3--z4; 
labels(1,2,3,4); 
endchar;

beginrunenchar("X",20);                  % This should be a "TH"
x1=x2=x3=x4=leftstemloc; x5=w-s; 
bot y1=-o; y2=.25h; y5=.5h; y3=.75h; top y4=h;
draw z1--z4;
draw z2--z5--z3;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("A",20); 
x1=x2=x3=leftstemloc; x4=x5=w-s; 
bot y1=-o; y2=.7h; y4=.5h; y5=.8h; top y3=h;
draw z1--z3--z5;
draw z2--z4;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("R",20); 
x1=x2=x3=leftstemloc; x4=x5=w-s; 
bot y1=bot y4=-o; y2=.4h; y5=.7h; top y3=h;
draw z1--z3--z5--z2--z4;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("K",20); 
x2=leftstemloc; x1=x3=w-s; 
y1=.4h; y2=.7h; top y3=h;
draw z1--z2--z3;
labels(1,2,3); 
endchar;

beginrunenchar("G",30); 
x1=x2=leftstemloc; x3=x4=w-s; 
bot y1=bot y3=-o; top y2=top y4=h;
draw z1--z4;
draw z2--z3;
labels(1,2,3,4); 
endchar;

beginrunenchar("W",20); 
x1=x2=x3=leftstemloc; x4=w-s; 
bot y1=-o; y2=.4h; y4=.7h; top y3=h;
draw z1--z3--z4--z2;
labels(1,2,3,4); 
endchar;

beginrunenchar("H",30); 
x1=x2=x3=leftstemloc; x4=x5=x6=w-s; 
bot y1=bot y4=-o; y2=.65h; y5=.35h; top y3=top y6=h;
draw z1--z3;
draw z4--z6;
draw z2--z5;
labels(1,2,3,4,5,6); 
endchar;

beginrunenchar("N",20); 
x1=leftstemloc; x2=x3=.5(w+s); x4=w-s; 
bot y2=-o; y1=.6h; y4=.4h; top y3=h;
draw z1--z4;
draw z2--z3;
labels(1,2,3,4); 
endchar;

beginrunenchar("I",10); 
x1=x2=leftstemloc; 
bot y1=-o; top y2=h;
draw z1--z2;
labels(1,2); 
endchar;

beginrunenchar("J",30); 
x1=leftstemloc; x2=.3w; x3=x4=.5(w+s); x5=.7w; x6=w-s; 
bot y5=-o; y1=.3h; y3=.4h; y4=.6h; y6=.7h; top y2=h;
draw z5--z1--z4;
draw z3--z6--z2;
labels(1,2,3,4,5,6); 
endchar;

beginrunenchar("Y",30);       % This should be an "EI" 
x1=leftstemloc; x2=x3=.5(w+s); x4=w-s; 
bot y2=-o; y1=.3h; y4=.7h; top y3=h;
draw z1--z2--z3--z4;
labels(1,2,3,4); 
endchar;

beginrunenchar("P",30); 
x1=x2=leftstemloc; x3=x4=.5(w+s); x5=x6=w-s; 
bot y1=bot y5=-o; y3=.3h; y4=.7h; top y2=top y6=h;
draw z5--z3--z1--z2--z4--z6;
labels(1,2,3,4,5,6); 
endchar;

beginrunenchar("Z",30); 
x1=leftstemloc; x2=x3=x4=.5(w+s); x5=w-s; 
bot y2=-o; y3=.6h; top y1=top y4=top y5=h;
draw z2--z4;
draw z1--z3--z5;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("S",20); 
x1=x2=leftstemloc; x3=x4=w-s; 
bot y1=-o; y3=.3h; y2=.7h; top y4=h;
draw z1--z3--z2--z4;
labels(1,2,3,4); 
endchar;

beginrunenchar("T",30); 
x1=leftstemloc; x2=x3=.5(w+s); x4=w-s; 
bot y2=-o; y1=y4=.7h; top y3=h;
draw z2--z3;
draw z1--z3--z4;
labels(1,2,3,4); 
endchar;

beginrunenchar("B",20); 
x1=x2=x3=leftstemloc; x4=x5=w-s; 
bot y1=-o; y4=.3h; y2=.5h; y5=.7h; top y3=h;
draw z1--z3--z5--z2--z4--z1;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("E",30); 
x1=x2=leftstemloc; x3=.5(w+s); x4=x5=w-s; 
bot y1=bot y5=-o; y3=.7h; top y2=top y4=h;
draw z1--z2--z3--z4--z5;
labels(1,2,3,4,5); 
endchar;

beginrunenchar("M",30); 
x1=x2=x3=leftstemloc; x4=x5=x6=w-s; 
bot y1=bot y4=-o; y2=y5=.4h; top y6=top y3=h;
draw z1--z3--z5;
draw z2--z6--z4;
labels(1,2,3,4,5,6); 
endchar;

beginrunenchar("L",20); 
x1=x2=leftstemloc; x3=w-s; 
bot y1=-o; y3=.6h; top y2=h;
draw z1--z2--z3;
labels(1,2,3); 
endchar;

beginrunenchar("Q",30);                  % And this should be an "ING"
x1=leftstemloc; x2=x3=.5(w+s); x4=w-s; 
y2=.4h; y1=y4=.7h; top y3=h;
draw z1--z3--z4--z2--z1;
labels(1,2,3,4); 
endchar;

beginrunenchar("D",30); 
x1=x2=leftstemloc; x3=x4=w-s; 
bot y1=bot y3=-o; top y2=top y4=h;
draw z1--z2--z3--z4--z1;
labels(1,2,3,4); 
endchar;

beginrunenchar("O",30);  
x1=x2=leftstemloc; x3=.5(w+s); x4=x5=w-s; 
bot y1= bot y4=-o; y2=y5=.7h; top y3=h;
draw z1--z5--z3--z2--z4;
labels(1,2,3,4,5); 
endchar;

beginrunenchar(":",10); 
x1=x2=x3=x4=leftstemloc; 
y1=.2h; y2=.35h; y3=.65h; y4=.8h;
draw z1--z2;
draw z3--z4;
labels(1,2,3,4); 
endchar;

font_coding_scheme:="FUXARKGWHNIJYPZSTBEMLQDO: only";

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  End of File
% 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
